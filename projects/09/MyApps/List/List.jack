class List {
    field int data;
    field List next;

    constructor List new(int car, List cdr) {
        let data =  car;
        let next = cdr;
        return this;
    }

    method int getData() {
        return data;
    }

    method List getNext() {
        return next;
    }

    method void print() {
        do Output.printInt(data);
        do Output.printChar(32); // asciiコードで 32 はスペース
        if (~(next = null)) {
            do next.print();
        }

        return;
    }

    method void dispose() {
        if (~(next = null)) {
            do next.dispose();
        }
        do Memory.deAlloc(this);
        
        return;
    }
}
