class Square {
    field int x, y, size;

    constructor Square new(int argX, int argY, int argSize) {
        let x = argX;
        let y = argY;
        let size = argSize;
        do draw();
        return this;
    }

    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    method void draw() {
        do Screen.setColor(true);
        do Screen.drawRectangle(x, y, x + size, y + size);
        return;
    }

    method void erase() {
        do Screen.setColor(false);
        do Screen.drawRectangle(x, y, x + size, y + size);
        return;
    }

    method void incSize() {
        if ((y + size + 2) < 256 & ((x + size + 2) < 512)) {
            do erase();
            let size = size + 2;
            do draw();
        }
        return;
    }

    method void decSize() {
        if ((size - 2) > 0) {
            do erase();
            let size = size - 2;
            do draw();
        }
        return;
    }

    method void moveUp() {
        if ((y - 2) > -1) {
            do Screen.setColor(false);
            do Screen.drawRectangle(x, (y + size) - 1, x + size, y + size);
            let y = y - 2;
            do Screen.setColor(true);
            do Screen.drawRectangle(x, y, x + size, y + 1);
        }
        return;
    }

    method void moveDown() {
        if ((y + size + 2) < 256) {
            do Screen.setColor(false);
            do Screen.drawRectangle(x, y, x + size, y + 1);
            let y = y + 2;
            do Screen.setColor(true);
            do Screen.drawRectangle(x, (y + size) - 1, x + size, y + size);
        }
        return;
    }

    method void moveLeft() {
        if ((x - 2) > -1) {
            do Screen.setColor(false);
            do Screen.drawRectangle((x + size) - 1, y, x + size, y + size);
            let x = x - 2;
            do Screen.setColor(true);
            do Screen.drawRectangle(x, y, x + 1, y + size);
        }
        return;
    }

    method void moveRight() {
        if ((x + size + 2) < 512) {
            do Screen.setColor(false);
            do Screen.drawRectangle(x, y, x + 1, y + size);
            let x = x + 2;
            do Screen.setColor(true);
            do Screen.drawRectangle((x + size) - 1, y, x + size, y + size);
        }
        return;
    }
}
