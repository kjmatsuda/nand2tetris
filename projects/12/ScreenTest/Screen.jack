// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Screen.jack

/**
 * A library of functions for displaying graphics on the screen.
 * The Hack physical screen consists of 256 rows (indexed 0..255, top to bottom)
 * of 512 pixels each (indexed 0..511, left to right). The top left pixel on 
 * the screen is indexed (0,0).
 */
class Screen {
    static boolean black;
    static int baseAddress;
    static int columnNum;
    static int rowNum;
    static Array twoToThe;
    
    /** Initializes the Screen. */
    function void init() {
        let black = true;
        let baseAddress = 16384;
        let columnNum = 512;
        let rowNum = 256;
        do Screen.clearScreen();

        let twoToThe = Array.new(16);
        let twoToThe[0] = 1;
        let twoToThe[1] = 2;
        let twoToThe[2] = 4;
        let twoToThe[3] = 8;
        let twoToThe[4] = 16;
        let twoToThe[5] = 32;
        let twoToThe[6] = 64;
        let twoToThe[7] = 128;
        let twoToThe[8] = 256;
        let twoToThe[9] = 512;
        let twoToThe[10] = 1024;
        let twoToThe[11] = 2048;
        let twoToThe[12] = 4096;
        let twoToThe[13] = 8192;
        let twoToThe[14] = 16384;
        let twoToThe[15] = twoToThe[14] + twoToThe[14];

        return;
    }

    /** Erases the entire screen. */
    function void clearScreen() {
        var int currentRow;
        var int currentColumn;

        let currentRow = 0;
        let currentColumn = 0;
        
        while (currentRow < rowNum) {
            while (currentColumn < columnNum) {
                do Memory.poke(baseAddress + (currentRow * 32) + (currentColumn / 16), 0);
                let currentColumn = currentColumn + 1;
            }
            let currentRow = currentRow + 1;
        }
        
        return;
    }

    /** Sets the current color, to be used for all subsequent drawXXX commands.
     *  Black is represented by true, white by false. */
    function void setColor(boolean b) {
        let black = b;
        return;
    }

    /** Draws the (x,y) pixel, using the current color. */
    function void drawPixel(int x, int y) {
        var int value;
        var int address;
        var int quotient;
        var int mod;
        
        let quotient = x / 16;
        let mod = x - (16 * quotient);
        
        let address = baseAddress + (y * 32) + (x / 16);
        
        let value = Memory.peek(address);

        if (black) {
            let value = value | twoToThe[mod];
        }
        else {
            // ビットを落とす
            let value = value & (~ twoToThe[mod]);
        }

        do Memory.poke(address, value);
        
        return;
    }

    /** Draws a line from pixel (x1,y1) to pixel (x2,y2), using the current color. */
    function void drawLine(int x1, int y1, int x2, int y2) {
        var int x, dx;
        var int xStep;
        var int y, dy;
        var int yStep;
        var int a, b;
        var int adyMinusbdx;
        
        let x = x1;
        let dx = x2 - x1;
        
        if (dx = 0) {
            let xStep = 0;
        }
        else {
            if (dx > 0) {
                let xStep = 1;
            } else {
                let xStep = -1;
            }
        }
        
        let y = y1;
        let dy = y2 - y1;
        if (dy = 0) {
            let yStep = 0;
        }
        else {
            if (dy > 0) {
                let yStep = 1;
            } else {
                let yStep = -1;
            }
        }
        
        let a = 0;
        let b = 0;
        let adyMinusbdx = 0;

        if ((dx = 0) & (dy = 0)) {
            do Screen.drawPixel(x, y);
        }
        else {
            while ((Math.abs(a) < Math.abs(dx + 1)) & (Math.abs(b) < Math.abs(dy + 1))) {
                do Screen.drawPixel(x + a, y + b);

                if (dx = 0) {
                    let b = b + yStep;
                } else {
                    if (dy = 0) {
                        let a = a + xStep;
                    }
                    else {
                        if (adyMinusbdx < 0) {
                            let a = a + xStep;
                            let adyMinusbdx = adyMinusbdx + Math.abs(dy);
                        } else {
                            let b = b + yStep;
                            let adyMinusbdx = adyMinusbdx - Math.abs(dx);
                        }                    
                    }
                }
            }
        }
        
        return;
    }

    /** Draws a filled rectangle whose top left corner is (x1, y1)
     * and bottom right corner is (x2,y2), using the current color. */
    function void drawRectangle(int x1, int y1, int x2, int y2) {
        var int dy;

        let dy = 0;
        while ((y1 + dy) < (y2 + 1)) {
            do Screen.drawLine(x1, y1 + dy, x2, y1 + dy);
            let dy = dy + 1;
        }
       
        return;
    }

    /** Draws a filled circle of radius r<=181 around (x,y), using the current color. */
    function void drawCircle(int x, int y, int r) {
        var int dy;
        var int squareR;
        var int sqrt;
        
        if (r > 181) {
            let r = 181;
        }

        let dy = -r;
        let squareR = (r * r);
        
        while (dy < (r + 1)) {
            let sqrt = Math.sqrt(squareR - (dy * dy));
            do Screen.drawLine(x - sqrt, y + dy, x + sqrt, y + dy);
            let dy = dy + 1;
        }
        
        return;
    }
}
